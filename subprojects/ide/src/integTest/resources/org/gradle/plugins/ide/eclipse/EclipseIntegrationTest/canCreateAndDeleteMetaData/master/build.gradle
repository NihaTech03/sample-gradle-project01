import org.gradle.plugins.ide.eclipse.model.AbstractClasspathEntry

allprojects {
    apply plugin: 'eclipse'

    group = 'org.gradle'

    plugins.withType(JavaBasePlugin) {
        sourceCompatibility = 1.5
    }
}

subprojects {
    repositories {
        mavenCentral()
    }

    group = 'org.gradle'
    version = '1.0'
}

configure(subprojects.findAll{ it.path in [ ':api', ':common', ':webAppJava6', ':webAppWithVars', ':webservice' ] }){
    apply plugin: 'eclipse-wtp'
}

allprojects {
    afterEvaluate { p ->
        configure(p) {
            if (p.hasProperty('eclipseClasspath')) {
                eclipse {
                    classpath {
                        downloadJavadoc = true
                        //this hack is it avoid getting mad with xmlunit failures for whitespace
                        //and with futile attempts to resolve them properly.
                        file.whenMerged { classpath ->
                            classpath.entries.each { entry ->
                                if (entry instanceof AbstractClasspathEntry) {
                                    entry.entryAttributes.remove(AbstractClasspathEntry.COMPONENT_NON_DEPENDENCY_ATTRIBUTE)
                                }
                            }
                        }
                    }
                }
            }
            cleanEclipse.doLast {
                assert !file(".classpath").exists()
                assert !file(".project").exists()
                assert !file('.settings').exists() || file('.settings').listFiles().length == 0
            }
        }
    }
}
